source mpSettings;

global proc AEmpMaterialBrowserResult( string $fileAttribute, string $filename, string $fileType )
{
	string $szWorkingDir = `workspace -q -dir`;
	optionVar -sv "mpVarMaterialDir" $szWorkingDir;

	// check if this file is in the base directory
	string $szBasePath = `optionVar -q "mpVarGlobalBasePath"`;

	// convert both paths to lower case to avoid case sensitivity
	$szBasePath	= `tolower $szBasePath`;
	$filename	= `tolower $filename`;

	// convert all back slashes to slashes
	$szBasePath	= substituteAllString($szBasePath, "\\", "/");

	// remove the base path
	if( startsWith($filename, $szBasePath) )
	{
		int $iLength1 = `size($filename)`;
		int $iLength2 = `size($szBasePath)`;

		$filename = endString($filename, ($iLength1 - $iLength2));
	}

	setAttr $fileAttribute -type "string" $filename;

	string $node;
	string $buffer[];
	tokenize($fileAttribute, ".", $buffer);
	$node = (size($buffer) > 0) ? $buffer[0] : "";

	// load the material using the plug-in
	mpMaterial -fl $filename $node;
}

global proc AEmpMaterialBrowser( string $cmd )
{
	if( `optionVar -exists "mpVarMaterialDir"` )
	{
		string $szWorkingDir = `optionVar -q "mpVarMaterialDir"`;
		workspace -dir $szWorkingDir;
	}

	string $szExtension = mpFileExtension( "mat" );

	//fileBrowserDialog -fl ("Material File (*." + $szExtension +  "),*." + $szExtension ) -m 0 -fc $cmd -ft ("*." + $szExtension ) -an "Select" -wt "Select Material" -ds 1;
	fileBrowserDialog -fl ("Material File (*." + $szExtension +  "),*." + $szExtension ) -m 0 -fc $cmd -ft ("*." + $szExtension ) -an "Select" -wt "Select Material" -ds 0;
}

global proc AEmpMaterialPathNew(string $fileAttribute)
{
	string $node;
	string $buffer[];
	tokenize($fileAttribute, ".", $buffer);
	$node = (size($buffer) > 0) ? $buffer[0] : "";

	if( `attributeExists "mpMaterialPath" $node` == 0 )
		addAttr -sn "mpMaterialPath" -ln "mpMaterialPath" -dt "string" -h 1 $node;

	setUITemplate -pst attributeEditorTemplate;
	rowLayout -nc 3 textureNameLayout;
		text -l "Material File";
		textField textureNameField;
			symbolButton -image "navButtonBrowse.xpm" browser;
	setParent ..;
	setUITemplate -ppt;
	
	AEmpMaterialPathReplace $fileAttribute;
}

global proc AEmpMaterialPathReplace(string $fileAttribute)
{
	string $node;
	string $buffer[];
	tokenize($fileAttribute, ".", $buffer);
	$node = (size($buffer) > 0) ? $buffer[0] : "";

	if( `attributeExists "mpMaterialPath" $node` == 0 )
		addAttr -sn "mpMaterialPath" -ln "mpMaterialPath" -dt "string" -h 1 $node;

	connectControl -fileName textureNameField $fileAttribute;

	string $command = "AEmpMaterialBrowserResult "+" "+$fileAttribute;

	button -e -c ("AEmpMaterialBrowser \"" + $command + "\"" ) browser;
}

global proc AEmpMaterialAttributes(string $node)
{
	editorTemplate -beginLayout "Monolith Attributes" -collapse true;
		editorTemplate -callCustom "AEmpMaterialPathNew" "AEmpMaterialPathReplace" "mpMaterialPath";
	editorTemplate -endLayout;
}
